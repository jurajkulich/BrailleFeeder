apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.android.braillefeeder"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }
    /*
    sourceSets {
        main {
            proto {
                srcDir 'src/main/proto/google/api'
                srcDir 'src/main/proto/google/logging'
                srcDir 'src/main/proto/google/longrunning'
                srcDir 'src/main/proto/google/rpc'
                srcDir 'src/main/proto/google/speech/v1'
                srcDir 'src/main/proto/google/type'
            }

        }
    }
    */
}

ext {
    grpcVersion = '1.14.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each {
            task -> task.plugins {
                javalite {}
                grpc {
                    option 'lite'
                }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/build/generated"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:support-annotations:27.1.1'

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Android things
    compileOnly 'com.google.android.things:androidthings:+'

    implementation 'com.squareup.okhttp3:okhttp:3.11.0'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.google.code.gson:gson:2.8.5'


    implementation project(':aars')
    implementation project(':models')

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation ("io.grpc:grpc-protobuf-lite:$grpcVersion")
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation 'javax.annotation:javax.annotation-api:1.2'

    protobuf 'com.google.protobuf:protobuf-java:3.6.1'

    implementation('com.google.auth:google-auth-library-oauth2-http:0.10.0') {
        exclude module: 'httpclient'
    }

    implementation ('com.google.api-client:google-api-client-android:1.23.0') {
        exclude module: 'guava-jdk5'
        exclude module: 'httpclient'
    }
    implementation ('com.google.http-client:google-http-client-gson:1.23.0') {
        exclude module: 'httpclient'
        exclude module: 'guava-jdk5'
    }
    implementation ('com.google.apis:google-api-services-vision:v1-rev369-1.23.0') {
        exclude module: 'guava-jdk5'
    }
}

task copySecretKey(type: Copy) {
    def File secretKey = file "$System.env.GOOGLE_APPLICATION_CREDENTIALS"
    from secretKey.getParent()
    include secretKey.getName()
    into 'src/main/res/raw'
    rename secretKey.getName(), "credentials.json"
}
preBuild.dependsOn(copySecretKey)

